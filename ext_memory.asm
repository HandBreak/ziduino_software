; TEST EXTENDED MEMORY

EX_MEM_TEST:
	ld a,0ffh		; (   ) Выбираем порт для которого воспроизводится чтение (A14=A15=1, A7=0/1 RESET)
	in a,(0bdh)		; Читаем из порта (подключаем свободную страницу памяти в область #C000) (по умолчанию дублируется с #0000-#4000)
	ld a,00000000b		; в A номер страницы (0/0/0)  (00000000)
	call EX_MEM_TST1	; Вызываем процедуру выбора и заполнения страниц
	ld a,00fh		; (   ) Выбираем порт для которого воспроизводится чтение (А14=A15=0, A7=0/1 RESET)
	in a,(0bdh)		; Читаем из порта (подключаем базовую страницу памяти в область #C000) (дублируется с #0000-#4000)
	ld a,10100000b		; в А номер страницы начиная со 2-ой (1/0/1) - дополнительно включаем DATA
	call EX_MEM_TST1	; Вызываем процедуру выбора и заполнения страниц
	ld a,0ffh		; (   ) Выбираем порт для которого воспроизводится чтение (A14=A15=1, A7=0/1 RESET)
	in a,(0bdh)		; Читаем из порта (подключаем свободную страницу памяти в область #C000) (по умолчанию дублируется с #0000-#4000)
	ld a,00000000b		; в A номер страницы (0/0/0)  (00000000)
	call EX_MEM_OUT1	; Вызываем процедуру вывода содержимого страниц
	ld a,00fh		; (   ) Выбираем порт для которого воспроизводится чтение (А14=A15=0, A7=0/1 RESET)
	in a,(0bdh)		; Читаем из порта (подключаем базовую страницу памяти в область #C000) (дублируется с #0000-#4000)
	ld a,00100000b		; в А номер страницы начиная со 2-ой (1/0/1) - дополнительно включаем DATA
	call EX_MEM_OUT1	; Вызываем процедуру вывода содержимого страниц
	ret
	
;END OF TEST	
	
EX_MEM_TST1:	
	out (0feh),a		; Выбираем страницу памяти
; Fill memory upper address #C000
	ld hl,0c000h		; Загружаем адрес начала очищаемого блока памяти
	ld de,0c001h		; Загружаем адрес следующей за ним ячейки блока
	ld bc,03fffh		; Загружаем размер блока-1
	ld (hl),a		; Пишем номер страницы в первый адрес блока
	ldir
	add 01000000b		; Увеличиваем номер страницы на 1
	jr nc,EX_MEM_TST1	; В случае переполнения цикл закончен, иначе повторяем
	ret

EX_MEM_OUT1:
	out (0feh),a		; Выбираем страницу памяти
	ld b,a
	ld hl,0c000h		; Загружаем адрес начала передаваемого блока
EX_MEM_OUT2:
	ld a,(hl)		; Читаем содержимое
	out (11110010b),a	; (#F2) Записываем его в порт данных FT245
	inc hl			; Вычисляем следующий адрес
	ld a,h			;
	or l			; Проверяем достижение конца области памяти
	jr nz,EX_MEM_OUT2	; Повторяем пока конец не достигнут
	ld a,b
	add 01000000b		; Увеличиваем номер страницы на 1
	jr nc,EX_MEM_OUT1	; В случае переполнения цикл закончен, иначе повторяем
	ret