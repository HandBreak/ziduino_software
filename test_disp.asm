OUTPUT_MESSAGES:
; ==============================
; TESTING DISPLAY FUNCTION     '
; ==============================
; continue programm. Main loop
	call INVERSE_SCREEN	; Инвертировать содержимое экрана (сделать черным)
	ld hl,0000h
	ld de,STRING1
	call PRN_STRING		; Выводим строку 1 в стандартном режиме
	ld de,STRING2
	set 7,l			; Устанавливаем режим инверсии символа
	call PRN_STRING		; Выводим строку 2 в инверсии
	ld de,STRING3
	set 3,l			; Устанавливаем режим поворота символа на 90`
	call PRN_STRING		; Выводим строку 3 в инверсии и с поворотом
	ld de,STRING4
	res 7,l			; Сбрасываем режим инверсии
	call PRN_STRING		; Выводим строку 4 с поворотом символов на 90`
	ld de,STRING5
	res 3,l			; Сбрасываем режим поворота
	set 7,l			; Устанавливаем режим инверсии
	call PRN_STRING		; Выводим строку 5 в инверсии
	res 7,l			; Сбрасываем инверсию
	xor a			; Обнуляем аккумулятор
	call SET_STARTLINE	; Устанавливаем в качестве верхней строки - область памяти 0-вого адреса
CNT_PRN:
; test smooth scroll loop	   Цикл плавного скроллинга
	ld bc,0			; обнуляем счетчик
ll:
	inc bc			; увеличиваем на единицу
	ld a,c
	or b			; проверяем достижение нуля
	jr nz, ll		; Если нуль не достигнут - повторяем цикл увеличения и проверки (пауза в 65536 циклов)
	ld a,l			; Загружаем в регистра "А" номер строки 
	or 7			; Устанавливаем его равным "7" (нижней строки), сохраняя биты атрибутов
	ld l,a			; Возвращаем результат в регистр 'L'
	ld a,(ix+upline_num)	; Загружаем в "A" установленный номер верхней строки
	push af			; Сохраняем его 
	call SET_STARTLINE	; Вызывем процедуру установки адреса верхней строки
	pop AF			; восстанавливаем номер верхней строки
	inc a			; увеличиваем его на 1
	ld (ix+upline_num),a	; сохраняем в переменной номера верхней строки
	and 7h			; оставляем только значащие биты номера строки (биты атрибутов сбрасываем)
	ccf			; инвертируем CY
; end test smooth scrool loop
	call z, PRN_STRING	; Если счетчик был кратен 8, выводим строку, иначе
	jr c, CNT_PRN		; Если при выводе строки нехватило места на экране, переходим к началу цикла скроллинга для вывода следующей строки
	ret			; Выйти из теста
	